#version 440

layout(local_size_x = 16) in;

#define ID gl_GlobalInvocationID.x

#include "planeetDefinities.glsl"

#define TIJD_VERSCHIL 	0.1
#define ZWAARTEKRACHT 	0.8
#define PIJP_DOORSNEE 	0.75
#define PIJP_LENGTE   	1.0

uniform float 	grondMult;


float hoogteverschil(uint buurID)
{
	return ((grondMult * vakken0[ID].grondHoogte) + vakken0[ID].waterHoogte) - ((grondMult * vakken0[buurID].grondHoogte) + vakken0[buurID].waterHoogte);
}

void main()
{
	float	fluxen		= 0.0f;
	uint 	aantalBuren = vakMetas[ID].burenAantal;

	for(uint buur=0; buur < aantalBuren && buur < 6; buur++)
	{
		uint 	buurID 	= vakMetas[ID].buren[buur];
		float 	flux 	= max(0, vakken0[ID].pijpen[buur] + (TIJD_VERSCHIL * PIJP_DOORSNEE * ( ( ZWAARTEKRACHT * hoogteverschil(buurID) ) / PIJP_LENGTE ) ) );


		vakken1[ID].pijpen[buur] = flux; 
		fluxen += flux;
	}

	memoryBarrier();

	float K = max(0, min(1, (PIJP_LENGTE * vakken0[ID].waterHoogte) / (fluxen * TIJD_VERSCHIL) ) );

	for(uint buur=0; buur < aantalBuren && buur < 6; buur++)
		vakken1[ID].pijpen[buur] = K * vakken1[ID].pijpen[buur];

}